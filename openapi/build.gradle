plugins {
  id 'java'
  id 'org.openapi.generator'
}

dependencies {
  compileOnly 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.35'
  compileOnly 'org.springframework.boot:spring-boot-starter-webflux:3.5.4'
  compileOnly 'jakarta.validation:jakarta.validation-api:3.1.1'
}

// https://github.com/OpenAPITools/openapi-generator/blob/master/modules/openapi-generator-gradle-plugin/README.adoc
openApiGenerate {
  generatorName = "spring"
  library = "spring-boot"
  inputSpec = "$projectDir/src/main/resources/api/api.yml"
  outputDir = "$buildDir/openapi"
  configOptions = [
    hideGenerationTimestamp: "true",
    openApiNullable: "false",
    skipDefaultInterface: "true",
    dateLibrary: "java8",
    interfaceOnly: "true",
    useTags: "true",
    useOptional: "true",
    reactive: "true",
    useSpringBoot3: "true"
  ]
  generateApiTests = false
  generateApiDocumentation = false
  generateModelTests = false
  generateModelDocumentation = false
  invokerPackage = "org.onap.portalng.preferences.openapi"
  apiPackage = "org.onap.portalng.preferences.openapi.api"
  modelPackage = "org.onap.portalng.preferences.openapi.model"
  modelNameSuffix = "ApiDto"
}

compileJava {
  dependsOn tasks.openApiGenerate
}

sourceSets {
  main {
    java {
      srcDirs += file("$buildDir/openapi/src/main/java")
    }
  }
}
tasks.withType(Test).configureEach {
  useJUnitPlatform()
}
